-- @path iStar=/iStar2Archi/Metamodels/iStar.ecore
-- @path Archi=/iStar2Archi/Metamodels/archimate.ecore

module iStar2Archi;
create OUT : Archi from IN : iStar;

-- Returns all actors direct goals (root goals)
helper context iStar!IstarDiagram def : getElements () : OrderedSet(iStar!IntentionalElement) =
	self.actors->iterate( element ; elements : OrderedSet(iStar!IntentionalElement) = 
		OrderedSet{} | 
			elements.append(element.contains)
		)
	;

rule iStar2Archi {
	from
		istar : iStar!IstarDiagram
	
	to
		archi : Archi!ArchimateDiagram (
			activestructureelement <- istar.actors,
			motivationelement <- istar.getElements()
			-- aqui teria que pegar todos os tasks e gravar juntxs listadinhos
		)
}

rule Actor2Stakeholder { -- Also transforms: iStar!Agent and iStar!Role to Archi!Stakeholder
	from 
		istar : iStar!Actor
	to
		archi : Archi!Stakeholder (
			name <- istar.name,
			target <- istar.contains
		)
}

rule Goal2Goal {
	from
		istar : iStar!Goal
	to
		archi : Archi!Goal (
			name <- istar.name,
			refinementType <- istar.refinementType,
			children <- istar.children,
			relationType <- 'association',
			influences <- istar.helps
		)
}

rule Task2BusinessProcess { 
	from
		istar : iStar!Task
	to
		archi : Archi!BusinessProcess (
			name <- istar.name,
			children <- istar.children,
			influences <- istar.helps,
			realizes <- if istar.refImmediateComposite().oclIsKindOf(iStar!Goal)
							then istar.parent
							else OclUndefined
							endif,
			relationType <- if istar.children.first().oclIsTypeOf(iStar!Task) 
								then 'composition' 
								else 'association' 
								endif,
			refinementType <- istar.refinementType
		)
}

rule Resource2Resources {
	from
		istar : iStar!Resource
	to
		archi : Archi!Resource (
			name <- istar.name,
			influences <- istar.helps,
			realizes <- if istar.refImmediateComposite().oclIsKindOf(iStar!Quality)
							then istar.parent
							else OclUndefined
							endif
		)
}

rule Quality2Principle {
	from
		istar : iStar!Quality
	to
		archi : Archi!Principle (
			name <- istar.name,
			influences <- istar.helps
		)
}

-- agora só falta ver se isso de um business process ficar dentro do goal é valido 
-- dar uma olhada em como funciona no metamodelo do archi
-- acho que o ideal seria tudo fora mesmo (mas ai nao sei como fazer)
